extends RefCounted

# Some providers consume native frame coordinates; others map a requested
# presentation surface back into their logical frame themselves.
static func input_surface_size(
    runtime_kind: String,
    content_size: Vector2,
    requested_surface: Vector2
) -> Vector2:
    if runtime_kind in ["minori", "onscripter", "rfvp"]:
        return content_size
    if requested_surface.x > 0.0 and requested_surface.y > 0.0:
        return requested_surface
    return content_size


# The frame texture defines the engine's pointer coordinate space. A runtime
# may accept a requested surface size before open and then replace it with the
# game's native size while booting, so the requested size must not be used to
# rescale input after a frame has been presented.
static func map_point(
    window_point: Vector2,
    target_rect: Rect2,
    texture_size: Vector2,
    clamp_to_bounds: bool = false
) -> Vector2:
    if texture_size.x <= 0.0 or texture_size.y <= 0.0:
        return Vector2(-1.0, -1.0)
    var scale := minf(
        target_rect.size.x / texture_size.x,
        target_rect.size.y / texture_size.y
    )
    if scale <= 0.0:
        return Vector2(-1.0, -1.0)
    var drawn_size := texture_size * scale
    var inside := window_point - target_rect.position - (
        target_rect.size - drawn_size
    ) * 0.5
    if (
        inside.x < 0.0
        or inside.y < 0.0
        or inside.x > drawn_size.x
        or inside.y > drawn_size.y
    ):
        if not clamp_to_bounds:
            return Vector2(-1.0, -1.0)
        inside = Vector2(
            clampf(inside.x, 0.0, drawn_size.x),
            clampf(inside.y, 0.0, drawn_size.y)
        )
    return inside / scale

static func map_delta(
    window_delta: Vector2,
    target_size: Vector2,
    texture_size: Vector2
) -> Vector2:
    if texture_size.x <= 0.0 or texture_size.y <= 0.0:
        return window_delta
    var scale := minf(
        target_size.x / texture_size.x,
        target_size.y / texture_size.y
    )
    return window_delta / maxf(0.0001, scale)


# Frame enhancement presents the game's logical frame directly, while the
# runtime continues to accept pointer events in its requested surface space.
# Artemis maps that surface to the logical frame with an aspect-fit viewport
# (including letterbox offsets), then maps input back through the same viewport.
# Mirror that transform here so the provider does not apply a second, different
# scale when it receives the event.
static func map_point_to_surface(
    window_point: Vector2,
    target_rect: Rect2,
    content_size: Vector2,
    surface_size: Vector2,
    clamp_to_bounds: bool = false
) -> Vector2:
    var content_point := map_point(
        window_point,
        target_rect,
        content_size,
        clamp_to_bounds
    )
    if content_point.x < 0.0 or content_point.y < 0.0:
        return content_point
    if surface_size.x <= 0.0 or surface_size.y <= 0.0:
        return content_point
    var surface_scale := minf(
        surface_size.x / content_size.x,
        surface_size.y / content_size.y
    )
    if surface_scale <= 0.0:
        return Vector2(-1.0, -1.0)
    var presented_size := content_size * surface_scale
    var surface_offset := (surface_size - presented_size) * 0.5
    return Vector2(
        surface_offset.x + content_point.x * surface_scale,
        surface_offset.y + content_point.y * surface_scale
    )


static func map_delta_to_surface(
    window_delta: Vector2,
    target_size: Vector2,
    content_size: Vector2,
    surface_size: Vector2
) -> Vector2:
    var content_delta := map_delta(window_delta, target_size, content_size)
    if (
        content_size.x <= 0.0
        or content_size.y <= 0.0
        or surface_size.x <= 0.0
        or surface_size.y <= 0.0
    ):
        return content_delta
    var surface_scale := minf(
        surface_size.x / content_size.x,
        surface_size.y / content_size.y
    )
    return Vector2(
        content_delta.x * surface_scale,
        content_delta.y * surface_scale
    )


# iOS reports sub-pixel finger drift even for an intentional tap. Once the
# gesture has stayed below the drag threshold, keep its press and release at
# the original contact point so a hover-driven runtime cannot reinterpret the
# tap as cursor motion.
static func stable_tap_point(
    down_point: Vector2,
    up_point: Vector2,
    drag_threshold: float
) -> Vector2:
    if up_point.distance_to(down_point) < maxf(0.0, drag_threshold):
        return down_point
    return up_point
